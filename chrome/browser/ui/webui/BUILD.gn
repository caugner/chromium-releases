# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//chromeos/dbus/config/use_real_dbus_clients.gni")
import("//components/metrics/generate_allowlist_from_histograms_file.gni")

generate_allowlist_from_histograms_file("webui_url_hashes") {
  namespace = "webui_metrics"
  input_xml_file = "//tools/metrics/histograms/enums.xml"
  tag = "enum"
  output_file = "webui_url_hashes.h"
  allow_list_name = "WebUIUrlHashes"
}

source_set("configs") {
  sources = [
    "chrome_untrusted_web_ui_configs.cc",
    "chrome_untrusted_web_ui_configs.h",
    "chrome_web_ui_configs.cc",
    "chrome_web_ui_configs.h",
  ]

  configs += [ "//build/config/compiler:wexit_time_destructors" ]

  deps = [
    "//chrome/browser/ui",
    "//chrome/browser/ui/webui",
    "//components/compose:buildflags",
    "//components/lens:buildflags",
    "//content/public/browser",
    "//extensions/buildflags:buildflags",
    "//printing/buildflags",
    "//skia",
    "//third_party/abseil-cpp:absl",
  ]

  if (is_win || is_mac || is_linux || is_chromeos) {
    deps += [
      "//chrome/browser/ui/lens",
      "//chrome/browser/ui/views/side_panel",
    ]
  }

  # Includes Ash Chrome and Lacros Chrome
  if (is_chromeos) {
    deps += [ "//chromeos/constants" ]
  }

  if (is_chromeos_ash) {
    sources += [
      "ash/chrome_untrusted_web_ui_configs_chromeos.cc",
      "ash/chrome_untrusted_web_ui_configs_chromeos.h",
      "ash/chrome_web_ui_configs_chromeos.cc",
      "ash/chrome_web_ui_configs_chromeos.h",
    ]

    deps += [
      "//ash/constants",
      "//ash/webui/color_internals",
      "//ash/webui/demo_mode_app_ui",
      "//ash/webui/diagnostics_ui",
      "//ash/webui/eche_app_ui",
      "//ash/webui/file_manager:file_manager_untrusted_ui",
      "//ash/webui/firmware_update_ui",
      "//ash/webui/focus_mode",
      "//ash/webui/graduation",
      "//ash/webui/help_app_ui",
      "//ash/webui/mall",
      "//ash/webui/os_feedback_ui",
      "//ash/webui/print_management",
      "//ash/webui/print_preview_cros",
      "//ash/webui/sanitize_ui",
      "//ash/webui/scanning",
      "//ash/webui/shimless_rma",
      "//ash/webui/shortcut_customization_ui",
      "//ash/webui/status_area_internals",
      "//ash/webui/vc_background_ui",
      "//chrome/browser/ash",
      "//chrome/browser/ash/annotator",
      "//chrome/browser/ash/app_mode",
      "//chrome/browser/ash/eche_app",
      "//chrome/browser/ash/login/demo_mode",
      "//chrome/browser/ash/mall",
      "//chrome/browser/ash/multidevice_debug",
      "//chrome/browser/ash/net",
      "//chrome/browser/ash/net/network_health",
      "//chrome/browser/ash/net/rollback_network_config",
      "//chrome/browser/ash/os_feedback",
      "//chrome/browser/ash/printing/print_management",
      "//chrome/browser/ash/scanning",
      "//chrome/browser/ash/shimless_rma",
      "//chrome/browser/ash/system_web_apps/apps/camera_app",
      "//chrome/browser/ash/system_web_apps/apps/media_app",
      "//chrome/browser/ash/system_web_apps/apps/personalization_app",
      "//chrome/browser/ash/system_web_apps/apps/projector_app",
      "//chrome/browser/ash/system_web_apps/apps/recorder_app",
      "//chrome/browser/ash/system_web_apps/apps/vc_background_ui",
      "//chrome/browser/ui/ash/holding_space",
      "//chrome/browser/ui/webui/ash/account_manager",
      "//chrome/browser/ui/webui/ash/app_install",
      "//chrome/browser/ui/webui/ash/arc_overview_tracing",
      "//chrome/browser/ui/webui/ash/arc_power_control",
      "//chrome/browser/ui/webui/ash/assistant_optin",
      "//chrome/browser/ui/webui/ash/audio",
      "//chrome/browser/ui/webui/ash/borealis_installer",
      "//chrome/browser/ui/webui/ash/cloud_upload",
      "//chrome/browser/ui/webui/ash/crostini_installer",
      "//chrome/browser/ui/webui/ash/crostini_upgrader",
      "//chrome/browser/ui/webui/ash/emoji",
      "//chrome/browser/ui/webui/ash/enterprise_reporting",
      "//chrome/browser/ui/webui/ash/extended_updates",
      "//chrome/browser/ui/webui/ash/healthd_internals",
      "//chrome/browser/ui/webui/ash/in_session_password_change",
      "//chrome/browser/ui/webui/ash/kerberos",
      "//chrome/browser/ui/webui/ash/launcher_internals",
      "//chrome/browser/ui/webui/ash/lock_screen_reauth",
      "//chrome/browser/ui/webui/ash/login",
      "//chrome/browser/ui/webui/ash/mako",
      "//chrome/browser/ui/webui/ash/manage_mirrorsync",
      "//chrome/browser/ui/webui/ash/multidevice_internals",
      "//chrome/browser/ui/webui/ash/multidevice_setup",
      "//chrome/browser/ui/webui/ash/notification_tester",
      "//chrome/browser/ui/webui/ash/office_fallback",
      "//chrome/browser/ui/webui/ash/parent_access",
      "//chrome/browser/ui/webui/ash/scalable_iph",
      "//chrome/browser/ui/webui/ash/sensor_info",
      "//chrome/browser/ui/webui/ash/settings",
      "//chrome/browser/ui/webui/ash/skyvault",
      "//chrome/browser/ui/webui/ash/smb_shares",
      "//chrome/browser/ui/webui/ash/sys_internals",
      "//chrome/browser/ui/webui/ash/vc_tray_tester",
      "//chrome/browser/ui/webui/ash/vm",
      "//chromeos/ash/components/kiosk/vision/webui",
      "//components/cross_device/logging",
    ]

    if (!is_official_build) {
      deps += [ "//ash/webui/sample_system_web_app_ui" ]
      if (!use_real_dbus_clients) {
        deps += [ "//chrome/browser/ui/webui/ash/emulator" ]
      }
    }
  }
}

source_set("webui") {
  sources = [
    "webui_util.cc",
    "webui_util.h",
  ]

  sources += get_target_outputs(":webui_url_hashes")

  deps = [
    ":webui_url_hashes",
    "//base",
    "//content/public/browser",
    "//content/public/common",
    "//services/network/public/mojom",
    "//ui/base",
    "//ui/resources",
  ]
  if (is_chromeos_lacros) {
    deps += [ "//chromeos/startup" ]
  }
}
