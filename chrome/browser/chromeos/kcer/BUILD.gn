# Copyright 2024 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")

assert(is_chromeos)

static_library("kcer") {
  sources = [
    "kcer_factory.cc",
    "kcer_factory.h",
  ]

  public_deps = [ "//chrome/browser:browser_public_dependencies" ]

  deps = [
    "//base",
    "//build:chromeos_buildflags",
    "//chrome/browser/profiles:profile",
    "//chromeos/components/kcer",
    "//components/keyed_service/core",
    "//content/public/browser",
    "//net",
  ]

  if (is_chromeos_ash) {
    deps += [ "//chromeos/ash/components/browser_context_helper" ]
  }

  if (is_chromeos_lacros) {
    sources += [
      "kcer_factory_lacros.cc",
      "kcer_factory_lacros.h",
    ]

    deps += [ "//chromeos/lacros" ]
  }
}

source_set("browser_tests") {
  testonly = true

  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

  sources = [ "kcer_factory_nss_browsertest.cc" ]

  deps = [
    ":kcer",
    "//base",
    "//base/test:test_support",
    "//chrome/browser",
    "//chrome/browser/profiles:profile",
    "//chrome/test:test_support",
    "//chrome/test:test_support_ui",
    "//chromeos/components/kcer",
    "//content/test:test_support",
    "//crypto:test_support",
    "//testing/gtest",
  ]
}

if (is_chromeos_lacros) {
  source_set("lacros_chrome_browsertests") {
    testonly = true

    defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

    sources = [ "kcer_factory_no_nss_lacros_browsertest.cc" ]

    deps = [
      ":kcer",
      "//base",
      "//base/test:test_support",
      "//chrome/browser",
      "//chrome/browser/profiles:profile",
      "//chrome/test:test_support",
      "//chrome/test:test_support_ui",
      "//chromeos/components/kcer",
      "//chromeos/crosapi/mojom",
      "//chromeos/lacros",
      "//content/test:test_support",
      "//testing/gtest",
    ]
  }
}
